#!/usr/bin/groovy

// Copyright (C) 2018 Luxoft Sweden AB

void buildARP(String variant, String imageName) {
    String arpDir = "meta-arp"
    String branchName = "master"

    // If this is a PR, CHANGE_TARGET will be set by the Pipeline Plugin,
    // otherwise check BRANCH_NAME which is set by Jenkins
    if (env.CHANGE_TARGET) {
        branchName = "${env.CHANGE_TARGET}"
    } else if (env.BRANCH_NAME) {
        branchName = "${env.BRANCH_NAME}"
    }

    node("Yocto") {
        dir(arpDir) {
            checkout scm
        }

        // Initialize pelux-manifests to get the code
        sh "rm -rf pelux-manifests/"
        sh "git clone https://github.com/Pelagicore/pelux-manifests.git -b ${branchName}"

        dir('pelux-manifests') {
            def code = load "ci-scripts/yocto.groovy"
            String arpPath = "${env.WORKSPACE}/${arpDir}"
            code.buildWithLayer(variant, imageName, arpDir, arpPath)
        }
    }
}

def variantMap = [:]
def variantList = env.VARIANT_LIST.split()

variantList.each {
    def list = it.split(":")
    variantMap["${list[0]}"] = {
        buildARP(list[0], list[1])
    }
}

parallel (variantMap)
